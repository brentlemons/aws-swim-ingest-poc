plugins {
	id 'org.springframework.boot' version '2.2.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
}

configurations {
    jaxb
}

group = 'com.awsproserve'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}


task genJaxb {
    ext.sourcesDir = "${buildDir}/generated-sources/jaxb"
    ext.classesDir = "${buildDir}/classes/jaxb"
    ext.schemaDir = "${projectDir}/src/main/resources/schemas"
    ext.bindingsDir = "${projectDir}/src/main/resources/schemas/bindings"

    outputs.dir classesDir

    doLast() {
        project.ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
                    classpath: configurations.jaxb.asPath
            mkdir(dir: sourcesDir)
            mkdir(dir: classesDir)

            System.setProperty('javax.xml.accessExternalSchema', 'all')
            System.setProperty('javax.xml.accessExternalDTD', 'all')
            
            xjc(destdir: sourcesDir) {
                schema(dir: schemaDir, includes: "**/*.xsd")
//            	binding(dir: bindingsDir, includes: "**/*.xjb")
                produces(dir: sourcesDir, includes: "**/*.java")
	            arg(value: "-extension")
            }

            javac(destdir: classesDir, source: 1.8, target: 1.8, debug: true,
                    debugLevel: "lines,vars,source",
                    classpath: configurations.jaxb.asPath) {
                src(path: sourcesDir)
                include(name: "**/*.java")
                include(name: "*.java")
            }

            copy(todir: classesDir) {
                fileset(dir: sourcesDir, erroronmissingdir: false) {
                    exclude(name: "**/*.java")
                }
            }
        }
    }
}

dependencies {
	implementation 'com.sun.xml.bind:jaxb-xjc:2.2.4'
	implementation 'org.springframework.boot:spring-boot-starter'
    implementation group: 'org.springframework', name:'spring-jms'
	implementation group: 'org.springframework', name: 'spring-oxm', version: '5.2.4.RELEASE'
    implementation group: 'org.projectlombok', name: 'lombok', version: '1.18.10'
	implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.10.3'

	implementation group: 'com.solacesystems', name: 'sol-jms', version: '10.6.4'
	implementation group: 'com.solacesystems', name: 'sol-jcsmp', version: '10.6.4'

	implementation group: 'software.amazon.awssdk', name: 'kinesis', version: '2.10.80'

    implementation(files(genJaxb.classesDir).builtBy(genJaxb))

    jaxb 'com.sun.xml.bind:jaxb-xjc:2.2.4'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

task buildZip(type: Zip) {
    from genJaxb
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtime
    }
}

//build.dependsOn genJaxb
build.dependsOn buildZip
